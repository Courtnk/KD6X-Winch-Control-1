/*
    Name:       KD6X Winch Control 1.ino
    Created:	3/1/2019 11:55:12 PM
    Author:     Courtney E. Krehbiel
*/

/*
  Code by Courtney Krehbiel KD6X to run a remote winch and motor by
  using a motor controller with speed set by using PWM and a filtered output.
  Controls are to allow the user to manipulate speed or sweep function.

  Feb. 13, 2019

 */


// Basic includes
#include "SPI.h"
#include "Adafruit_GFX.h"
#include "Adafruit_ILI9341.h"
#include "Ethernet.h"


// Fonts to use
#include <Fonts/FreeMono9pt7b.h>
#include <Fonts/FreeMonoBold12pt7b.h>

#include <EEPROM.h>


// For the Adafruit shield, these are the default.
#define TFT_DC 9
#define TFT_CS 10

// Use hardware SPI (on Uno, #13, #12, #11) and the above for CS/DC
// On Mega, SPI is #50, #51, #52.  Clk = 52, MISO = 50, MOSI = 51
Adafruit_ILI9341 tft = Adafruit_ILI9341(TFT_CS, TFT_DC);

const byte ButtonInterrupt1Pin = 3;	// Interrupt 1 input to handle soft button pushes
const byte ButtonInterrupt0Pin = 2;	// Interrupt 0 input to handle shaft rotation

const byte LimitInput = 8;			// Pin to use to read status of Limit switches

volatile unsigned char Lockout = 0;	// Used to lockout interrupts for a few loops to debounce the pushbuttons

const int ButtonMap[] = { A2, 5, 6, 7, 11, 12 };  // Array to convert button number to actual pin number

volatile byte ButtonPushed[] = { HIGH, HIGH, HIGH, HIGH, HIGH, HIGH };	// Place to store status of buttons returned by interrupt
																		// Remember that lines are neutral pulled up HIGH
																		// So LOW is active push !!!!

volatile byte ActiveScreen[] = { LOW, LOW, LOW, LOW, LOW, LOW };  // Array to store current screen display and corresponding button assignments
// ActiveScreen [0] = Home
// ActiveScreen [1] = Setup
// ActiveScreen [2] = Setup Ht
// ActiveScreen [3] = Setup Spd
// ActiveScreen [4] = Run
// ActiveScreen [5] = Manual


	
int SpeedPin = 13;		// Speed PWM filter circuit connected to digital pin 13.  Only works on Mega, not Uno

int mspeed = 0;			// Actual speed for motor
int SetSpeed = 50;		// Set speed in computer.  
int mPWM = 0;

byte VOn48 = LOW;			// Is the 48 volt supply enabled; red if off, green if on
byte Relay1On = LOW;		// Relay 1 on relay shield.  LOW = 48V off and relay inactive.  HIGH = 48V on and relay active.
byte RunActive = LOW;		// Is the winch active; grey if inactive, yellow if active
byte Limit = HIGH;				// Has a limit switch been tripped; green if not tripped (HIGH), red if tripped (LOW)

byte RunningSlow = LOW;			// Internal indicator that motor speed has been programatically reduced as target height is approached.
int SlowSpeed = 20;				// Planned slow speed

float RunHeight = 0.00f;		// Running height
float ShaftCount;				// Counter to keep track of shaft rotations
float OldShaftCount;			// Previous value of ShaftCount
float SetHeight = 20.00f;		// Height target that's set by setup
byte Direction = LOW;			// 1 = up, 0 = down
byte LimitDirection;			// Stores the direction of the limit hit.  Going up or down.
byte LimitMode = LOW;			// Limit mode LOW = Read input; mode HIGH = Write Output;
int NeedMotorTurns;				// Counter to count a few turns of the motor to clear limit condition.
byte GlitchReset = LOW;			// Flag to note that height positioning may be reset
byte Step1 = HIGH;				// Flags for each of the limit processing steps
byte Step2 = LOW;
byte Step3 = LOW;
byte AnyButtonPush = HIGH;		// Flag to allow any button push to continue

float Speed = 0.00f;			// Feet per minute of system.  TBD depending on gearing

long eeAddress = 0;				// Location where height to be stored in EEPROM
								// 4096 bytes of EEPROM on Mega, and each good for about 100,000 writes.

// Variables for serial communication
int SerHeight; 
int SerSpeed;



// Setup initial stuff
void setup() {

	Serial.begin(9600);		// Initialize serial for troubleshooting
	
	tft.begin();	// Initialize display required

	// Initialize RunHeight from EEPROM
	randomSeed(analogRead(7));		// Ramdom number seed by reading unused port
	eeAddress = random(10,4000);		// select a random address to avoid overusing one area of EEPROM
	Serial.println(F("Random EEPROM address: "));  // Lets take a look
	Serial.println(eeAddress); 
	/*
	EEPROM.put(0, 0.00);	// Temporary Reset of count when active
	*/
	EEPROM.get(0, ShaftCount);	// Get last specially stored height from location 0.
	EEPROM.put(eeAddress, ShaftCount);	// Copy it to the randomly chosen location as starting point

	// Initialize outputs to motor controller
	pinMode(A0, OUTPUT);  // Initialize relay driving pin as output.  Using analog pin as digital output to drive 48V relay
	pinMode(A1, OUTPUT);	//  Initialize relay driving pin as output.  Driving motor control up/down

	// Intialize input from limit switches
	pinMode(8, INPUT);		// Hi-Z input

	// Initialize softbuttons around display
	pinMode(12, INPUT_PULLUP);    // Nominally +Increase sign	Button 5
	pinMode(11, INPUT_PULLUP);    // Nominally -Decrease sign	Button 4
	pinMode(7, INPUT_PULLUP);    // Far right button			Button 3
	pinMode(6, INPUT_PULLUP);    // 2nd from right button		Button 2
	pinMode(5, INPUT_PULLUP);    // 2nd from left button		Button 1
	// pinMode(4, INPUT_PULLUP);    // Far left button			Button 0   Replaced due to conflict with Ethernet shield  See ButtonMap line 46
	pinMode(A2, INPUT_PULLUP);	 // Far left button				Button 0

	tft.setRotation(3);  // Set screen to landscape with connections in upper left

	tft.fillScreen(ILI9341_NAVY);

	DisplayHome();			// Display the home screen
	ActiveScreen[0] = HIGH; // Keep track of active screen
	
	SetMotorSpeed(0,0);		// Get PWM going.  Output should be about 1 volt which is just at motor off threshold. 

	// Set interrupt to handle soft button pushes
	pinMode(ButtonInterrupt1Pin, INPUT_PULLUP);
	attachInterrupt(digitalPinToInterrupt(ButtonInterrupt1Pin), ButtonPush, RISING);

	// Set interrupt to handle shaft rotation counting
	pinMode(ButtonInterrupt0Pin, INPUT);
	attachInterrupt(digitalPinToInterrupt(ButtonInterrupt0Pin), ShaftRotation, RISING);

}


// Loop that runs continuously awaiting interrupts and other actions
void loop() {

	SerialCheck();		// Check to see if any serial port input is coming and process accordingly

	HomeActions();		// Implement Home screen softbutton actions

	ManualActions();	// Implement Manual screen softbutton actions

	Setup1Actions();	// Implement Setup1 screen softbutton actions

	SetupHeightActions(); // Implement Setup Height screen softbutton actions

	SetupSpeedActions();  // Implement Setup Speed screen softbutton actions

	RunActions();			// Implement Run Screen softbutton actions


	if (Lockout > 0) {
		Lockout = --Lockout;	// Decrement the lockout counter
	}

	delay(30);			// Assume other stuff will take some time, so don't loop constantly

	CheckLimit();		// Check status of limit switch, and act accordingly
	
}

/*
======================================= Interrupt Routines Follow =======================================
*/

void ButtonPush() {		// Process button push interrupt
	byte val;
	int i;

	if (Lockout > 0 && AnyButtonPush) {		// Skip interrupt processing while Locked out for debounce.  Have to create an exception for AnyButtonPush halt state.
		return;
	}

	// Otherwise proceed with interrupt processing
	// Which button was pushed?
	for (i = 0; i < 6; i++) {
		val = digitalRead(ButtonMap[i]);
		if (!val) {
			ButtonPushed[i] = LOW;		// Remember that active button is low!
		}
	}

	Lockout = 5;		// Setup lockout for a short time.  May need to adjust this later as loop delay increases
	AnyButtonPush = HIGH;	// Reset this once a button has been pushed
}

void ShaftRotation() {	// Increment or decrement a counter each time the motor shaft rotates.  Plus check for positioning versus limits and settings.

//	Serial.println(millis());		// Put in to check interrupt processing time.  Currently less than 1 mSec.
	OldShaftCount = ShaftCount;
	if (Direction) {				// Going Up
		ShaftCount += 1;
		if (!RunningSlow) {						// Skip this if system is already running slow
			if ((SetHeight - RunHeight) < 1) {	// If not running slow, check to see if target height is within a foot
				SetMotorSpeed(SlowSpeed, SetSpeed);		// Slow down the motor
				RunningSlow = HIGH;						// Set the indicator flag for slow speed operation
			}
		}
		if (RunHeight >= (SetHeight-0.26) || RunHeight >= 26.74) {			// Want to stop motor immediately when it gets within 1 shaft rotation of target
			mspeed = 0;
			if (RunningSlow) {
				SetMotorSpeed(mspeed, SlowSpeed);
			}
			else {
				SetMotorSpeed(mspeed, SetSpeed);
			}
			RunActive = LOW;
			DrawLights();
		}
	}
	else {
		ShaftCount -= 1;			// Going Down
		if (!RunningSlow) {						// Skip this if system is already running slow
			if ((RunHeight - SetHeight) < 1) {				// If not running slow, check to see if target height is within a foot
				SetMotorSpeed(SlowSpeed, SetSpeed);		// Slow down the motor
				RunningSlow = HIGH;						// Set the indicator flag for slow speed operation
			}
		}
		if (RunHeight <= (SetHeight+0.26) || RunHeight <= 10.26) {	// Want to stop motor immediately
			mspeed = 0;
			if (RunningSlow) {
				SetMotorSpeed(mspeed, SlowSpeed);
			}
			else {
				SetMotorSpeed(mspeed, SetSpeed);
			}
			RunActive = LOW;
			DrawLights();
		}
	}



	EEPROM.put(eeAddress, ShaftCount);		// Temporarily store ShaftCount in randomly chosen EEPROM address.  Do I need this?  How would I use it?
	DisplayCurrentHeight(ShaftCount);		// Hopefully this doesn't bog down the interrupt handling

	if (NeedMotorTurns) {					// If we're recovering from a limit and just need a few revs, this will be >0  ie: 3
		NeedMotorTurns -= 1;				// Decrement the counter
		
	}
//	Serial.println(millis());
}


/*
======================================= Subroutines Follow =======================================
*/

// Serial Check Subroutine -- Check to see if serial port input is available and parse/use accordingly
byte SerialCheck() {
	while (Serial.available() > 0) {
		// Look for first valid integer in the incoming serial stream
		SerHeight = Serial.parseInt();
		if (SerHeight < 0 || SerHeight > 27) {
			SerHeight = 10;
		}
		// Do it again
		SerSpeed = Serial.parseInt();
		if (SerSpeed < 0 || SerSpeed > 99) {
			SerSpeed = 50;
		}

		// Look for the newline.  That's the end of the sentence
		if (Serial.read() == '\n') {
			SetHeight = SerHeight;
			SetSpeed = SerSpeed;
			// Let's print the data on the screen for a double check
			// Serial.println(F("SetHeight = "));  
			// Serial.println(SetHeight);
			// Serial.println(F("SetSpeed = "));
			// Serial.println(SetSpeed);

			// Now let's update the display
			DisplayUpdate();
		}
	}
}


/* Programming Actions for Screens */

// Programming actions for Home Screen
byte HomeActions() {
	// Actions for Home Screen Softbuttons

	if ((!ButtonPushed[0]) & ActiveScreen[0]) {		// Button 0 selects Setup1 screen
		ResetActiveScreen();
		ActiveScreen[1] = HIGH;
		DisplaySetup1();
		ResetButtons();
	}

	if ((!ButtonPushed[1]) & ActiveScreen[0]) {		// Button 1 selects Run screen
		ResetActiveScreen();
		ActiveScreen[4] = HIGH;
		DisplayRun();
		ResetButtons();
	}

	if ((!ButtonPushed[3]) & ActiveScreen[0]) {		// Button 3 selects the Manual Screen
		ResetActiveScreen();
		ActiveScreen[5] = HIGH;
		DisplayManual();
		ResetButtons();
	}

}

// Programming actions for Manual Screen
byte ManualActions() {
	// Actions for Manual Screen Softbuttons

	if ((!ButtonPushed[0]) & ActiveScreen[5]) {		// Switch to Home Screen
		if (!RunActive) {		// Can't switch screens if motor is running
			ResetActiveScreen();
			ActiveScreen[0] = HIGH;
			mspeed = 0;		// Set speed to zero if leaving this screen
			RunActive = LOW;
			Direction = LOW;
			DisplayHome();
			ResetButtons();
		}
	}

	if ((!ButtonPushed[5]) & ActiveScreen[5]) {		// Increase programmed motor speed
		if (!RunActive) {
			if (SetSpeed <= 95)
			{
				SetSpeed += 5;
			}
			else
			{
				SetSpeed = 100;
			}
			DisplayMotorSpeed(SetSpeed, 35, 135);
		}
		ResetButtons();
	}

	if ((!ButtonPushed[4]) & ActiveScreen[5]) {		// Decrease programmed motor speed
		if (!RunActive) {
			if (SetSpeed >= 5)
			{
				SetSpeed -= 5;
			}
			else
			{
				SetSpeed = 0;
			}
			DisplayMotorSpeed(SetSpeed, 35, 135);
		}
		ResetButtons();
	}

	if ((!ButtonPushed[1]) & ActiveScreen[5]) {		// Flip 48V status
		if (!RunActive) {							// Can't turn off 48V if motor is running
			VOn48 = !VOn48;
			DrawLights();
			Relay1On = !Relay1On;		// Flip Relay1 State to turn 48V on or off.  Default is off.
			digitalWrite(A0, Relay1On);
		}
		ResetButtons();
	}

	if ((!ButtonPushed[2]) & ActiveScreen[5]) {		// Flip RunActive status
		if (VOn48) {								// Only become active if 48V is on
			RunActive = !RunActive;
			DrawLights();
			ResetButtons();
			if (RunActive) {
				mspeed = SetSpeed;
				RunningSlow = LOW;						// Assume we want full speed when first activated; of course with usual startup ramp
				SetMotorSpeed(mspeed, SetSpeed);
			}

		
			else {										// We want to stop the motor.  Need to check whether it's running full speed, or at slow speed to apply correct ramp.
				mspeed = 0;
				if (RunningSlow) {
					SetMotorSpeed(mspeed, SlowSpeed);
				}
				else {
					SetMotorSpeed(mspeed, SetSpeed);
				}
			}
		}
	}


	if ((!ButtonPushed[3]) & ActiveScreen[5]) {		// Flip Up/Down status
		if (!RunActive) {							// Can't change direction while motor is running
			Direction = !Direction;					// Default direction is LOW which is Down
			digitalWrite(A1, Direction);			// Set direction on motor controller
			tft.fillRect(35, 110, 200, 25, ILI9341_NAVY);  // Erase old direction first.  Note that args are (x, y, len x, len y)
			tft.setCursor(40, 125);
			tft.setTextSize(0);
			tft.setFont(&FreeMono9pt7b);
			tft.setTextColor(ILI9341_WHITE);
			if (!Direction) {
				tft.println(F("Direction:   Down"));
			}
			else {
				tft.println(F("Direction:   Up"));
			}
		}
		ResetButtons();
	}
}

// Programming actions for Setup1 Screen
byte Setup1Actions() {
	// Actions for Setup1 Screen Softbuttons

	if ((!ButtonPushed[0]) & ActiveScreen[1]) {		// Switch to Home Screen
		ResetActiveScreen();
		ActiveScreen[0] = HIGH;
		DisplayHome();
		ResetButtons();
	}
	
	if ((!ButtonPushed[1]) & ActiveScreen[1]) {		// Setup height screen
		ResetActiveScreen();	// Setting height goes here
		ActiveScreen[2] = HIGH;
		DisplaySetupHeight();
		ResetButtons();
	}

	if ((!ButtonPushed[2]) & ActiveScreen[1]) {		// Setup Max Speed screen
		ResetActiveScreen();	// Setting max speed goes here
		ActiveScreen[3] = HIGH;
		DisplaySetupSpeed();		// Setup max speed goes here
		ResetButtons();
	}
}

// Programming actions for SetupHeight Screen
byte SetupHeightActions() {
	// Actions for Setupheight Screen Softbuttons

	if ((!ButtonPushed[0]) & ActiveScreen[2]) {		// Switch to Home Screen
		NullGlitchReset();		// If any button other than 3 is pressed, be sure to anul the glitch reset flag
		ResetActiveScreen();
		ActiveScreen[0] = HIGH;
		DisplayHome();
		ResetButtons();
	}

	if ((!ButtonPushed[1]) & ActiveScreen[2]) {		// Back up to previous screen
		// Code to backup to previous screen
		NullGlitchReset();
		ResetActiveScreen();
		ActiveScreen[1] = HIGH;
		DisplaySetup1();
		ResetButtons();
	}

	if ((!ButtonPushed[2]) & ActiveScreen[2]) {		// Go to RUN screen
		// Code to switch to RUN screen 
		NullGlitchReset();
		ResetButtons();
	}

	if ((!ButtonPushed[3]) & ActiveScreen[2]) {		// Reset ShaftCount -- REQUIRES 2 SUCCESSIVE PUSHES OF BUTTON
	// Code to reset ShaftCount to 0 in case of glitch
		if (!GlitchReset) {		// LOW state meaning this is first push
			tft.setTextColor(ILI9341_ORANGE);  // Set a note that 2nd push required to reset Current Height to zero
			tft.setFont(&FreeMono9pt7b);
			tft.setTextSize(0);
			tft.setCursor(1, 190);
			tft.println(F("Press ResCHt to zero Cur.Ht."));
			GlitchReset = HIGH;
		}
		else {		// This is second push, so reset ShaftCount to 0;
			NullGlitchReset();		// Reset the indicator byte and erase any possible messages on screen
			ShaftCount = 0.00;
			tft.setTextColor(ILI9341_WHITE);	// Send an assurance message
			tft.setCursor(1, 190);   
			tft.println(F("Current Height reset"));
			delay(1000);
			tft.setTextColor(ILI9341_NAVY); 
			tft.setCursor(1, 190);
			tft.println(F("Current Height reset"));		// Erase by printing with background
			}
		ResetButtons();
	}

	if ((!ButtonPushed[5]) & ActiveScreen[2]) {		// Increase height setting
		NullGlitchReset();
		if (SetHeight <= 26)	{
			SetHeight += 1;
		}
		DisplaySetHeight(SetHeight);
		ResetButtons();
	}

	if ((!ButtonPushed[4]) & ActiveScreen[2]) {		// Decrease height setting
		NullGlitchReset();
		if (SetHeight >= 11)	{
			SetHeight -= 1;
		}
		
		DisplaySetHeight(SetHeight);
		ResetButtons();
	}
}

// Programming actions for Run Screen
byte RunActions() {
	// Actions for Run Screen Softbuttons

	if ((!ButtonPushed[0]) & ActiveScreen[4]) {		// Switch to Home Screen
		ResetActiveScreen();
		ActiveScreen[0] = HIGH;
		DisplayHome();
		ResetButtons();
	}

	if ((!ButtonPushed[1]) & ActiveScreen[4]) {		// Button 0 selects Setup1 screen
		ResetActiveScreen();
		ActiveScreen[1] = HIGH;
		DisplaySetup1();
		ResetButtons();
	}

	if ((!ButtonPushed[5]) & ActiveScreen[4]) {		// Button 0 selects Setup1 screen
		ResetActiveScreen();
		ActiveScreen[0] = HIGH;
		EmergencyHalt();		// Run emergency halt routine.  Return here when any button pushed.
		DisplayHome();
		ResetButtons();
	}
	   
}

// Programming actions for SetupSpeed Screen
byte SetupSpeedActions() {
	// Actions for SetupSpeede Screen Softbuttons

	if ((!ButtonPushed[0]) & ActiveScreen[3]) {		// Switch to Home Screen
		ResetActiveScreen();
		ActiveScreen[0] = HIGH;
		DisplayHome();
		ResetButtons();
	}

	if ((!ButtonPushed[1]) & ActiveScreen[3]) {		// Back up to previous screen
		// Code to backup to previous screen
		ResetActiveScreen();
		ActiveScreen[1] = HIGH;
		DisplaySetup1();
		ResetButtons();
	}

	if ((!ButtonPushed[2]) & ActiveScreen[3]) {		// Go to RUN screen
		// Code to switch to RUN screen 
		ResetButtons();
	}

		if ((!ButtonPushed[5]) & ActiveScreen[3]) {		// Increase speed setting
		if (SetSpeed < 95) {
			SetSpeed += 5;
		}
		else if (SetSpeed >= 95) {
			SetSpeed = 99;
		}
		DisplaySetSpeed(SetSpeed);
		ResetButtons();
	}

	if ((!ButtonPushed[4]) & ActiveScreen[3]) {		// Decrease speed setting
		if (SetSpeed == 99) {
			SetSpeed = 95;
		}
		else if (SetSpeed >= 5) {
			SetSpeed -= 5;
		}
		DisplaySetSpeed(SetSpeed);
		ResetButtons();
	}
}


/* Screen Displays*/

// Display the Home Screen
byte DisplayHome() {

	int Indent2 = 40;
	int BotLine = 216;
	int StatusTop = 75;

	tft.fillScreen(ILI9341_NAVY);  // Clear old screen

	DrawLights();		// Draw indicator lights at top right on screen
	DrawButtons();		// Draw soft button outlines on display

	tft.setTextColor(ILI9341_WHITE);  // Preliminary text setup
	tft.setFont(&FreeMonoBold12pt7b);
	tft.setTextSize(0);

	tft.setCursor(25, StatusTop);
	tft.println(F("Status:"));		// Large font just for header

	tft.setFont(&FreeMono9pt7b);	// Switch to smaller font for all else
	tft.setCursor(10, 15);
	tft.println(F("Home"));

	tft.setCursor(Indent2, StatusTop+25);		// Print home status elements
	CalcRunHeight();
	sprintfloat("Current Ht: ", RunHeight);		// Call special function that will parse float and print to screen

	tft.setCursor(Indent2, StatusTop+50);
	sprintfloat("Set Height: ", SetHeight);
	
	tft.setCursor(Indent2, StatusTop+75);
	if (!Direction) {
		tft.println(F("Direction:   Down"));
	}
	else {
		tft.println(F("Direction:   Up"));
	}
	
	tft.setCursor(Indent2, StatusTop+100);
	SprintAnInt("Speed 0-99%:", SetSpeed);		// Routine will do a conversion of speed to int

	tft.setTextColor(ILI9341_YELLOW);  // set color

	tft.setCursor(11, BotLine);		// Now display softbutton labels at bottom
	tft.println(F("SETUP"));
	tft.setCursor(100, BotLine);
	tft.println(F("RUN"));
	tft.setCursor(242, BotLine);
	tft.println(F("MANUAL"));

}

// Display the Manual Screen
byte DisplayManual() {

	int Indent2 = 40;
	int BotLine = 216;
	int StatusTop = 75;

	tft.fillScreen(ILI9341_NAVY);  // Clear old screen

	DrawLights();		// Draw indicator lights at top right on screen
	DrawButtons();		// Draw soft button outlines on display

	tft.setTextColor(ILI9341_WHITE);  // Preliminary text setup
	tft.setFont(&FreeMonoBold12pt7b);
	tft.setTextSize(0);

	tft.setCursor(25, StatusTop);
	tft.println(F("Status:"));		// Large font just for header

	tft.setFont(&FreeMono9pt7b);	// Switch to smaller font for all else
	tft.setCursor(10, 15);
	tft.println(F("Manual"));

	tft.setCursor(Indent2, StatusTop + 25);		// Print home status elements
	CalcRunHeight();
	sprintfloat("Current Ht: ", RunHeight);		// Call special function that will parse float and print to screen

	tft.setCursor(Indent2, StatusTop + 50);
	if (!Direction) {
		tft.println(F("Direction:   Down"));
	}
	else {
		tft.println(F("Direction:   Up"));
	}

	tft.setCursor(Indent2, StatusTop + 75);
	SprintAnInt("Speed 0-100:", SetSpeed);

	tft.setTextColor(ILI9341_YELLOW);  // set color

	tft.setCursor(16, BotLine);		// Now display softbutton labels at bottom
	tft.println(F("HOME"));
	tft.setCursor(100, BotLine);
	tft.println(F("48V"));
	tft.setCursor(165, BotLine);
	tft.println(F("RUN/ST"));
	tft.setCursor(248, BotLine);
	tft.println(F("UP/DN"));

	tft.setCursor(265, 125);	// Display softbutton side labels
	tft.println(F("SPEED"));
	tft.setTextSize(2);		// Switch to larger font size for + and -
	tft.setCursor(282, 88);	
	tft.println(F("+"));
	tft.setCursor(282, 168);
	tft.println(F("-"));

}

// Display the Setup 1 Screen
byte DisplaySetup1() {

	int Indent2 = 40;
	int BotLine = 216;
	int StatusTop = 75;

	tft.fillScreen(ILI9341_NAVY);  // Clear old screen

	DrawLights();		// Draw indicator lights at top right on screen
	DrawButtons();		// Draw soft button outlines on display

	tft.setTextColor(ILI9341_WHITE);  // Preliminary text setup
	tft.setFont(&FreeMonoBold12pt7b);
	tft.setTextSize(0);

	tft.setCursor(25, StatusTop + 20);
	tft.println(F("Change Setting:"));		// Large font just for header

	tft.setFont(&FreeMono9pt7b);	// Switch to smaller font for all else
	tft.setCursor(10, 15);
	tft.println(F("Setup"));

	tft.setCursor(Indent2, StatusTop + 45);		// Print home status elements
	sprintfloat("Set Height: ", SetHeight);		// Call special function that will parse float and print to screen

	tft.setCursor(Indent2, StatusTop + 70);
	SprintAnInt("Set Speed:  ", SetSpeed);
	
	tft.setTextColor(ILI9341_YELLOW);  // set color

	tft.setCursor(16, BotLine);		// Now display softbutton labels at bottom
	tft.println(F("HOME"));
	tft.setCursor(86, BotLine);
	tft.println(F("HEIGHT"));
	tft.setCursor(169, BotLine);
	tft.println(F("SPEED"));
}

// Display the Setup Height Screen
byte DisplaySetupHeight() {

	int Indent2 = 40;
	int BotLine = 216;
	int StatusTop = 75;

	tft.fillScreen(ILI9341_NAVY);  // Clear old screen

	DrawLights();		// Draw indicator lights at top right on screen
	DrawButtons();		// Draw soft button outlines on display

	tft.setTextColor(ILI9341_WHITE);  // Preliminary text setup
	tft.setFont(&FreeMonoBold12pt7b);
	tft.setTextSize(0);

	tft.setCursor(20, StatusTop + 20);
	tft.println(F("Edit Max Height:"));		// Large font just for header

	tft.setCursor(70, 165);		// Write Current Height Setting on screen while in big font
	tft.setTextSize(2);
	sprintfloat("", SetHeight);
		

	tft.setFont(&FreeMono9pt7b);	// Switch to smaller font for all else
	tft.setTextSize(0);
	tft.setCursor(10, 15);
	tft.println(F("Setup Height"));

	tft.setCursor(18, StatusTop + 45);
	tft.println(F("(10ft to Max 27ft)"));

	tft.setTextColor(ILI9341_YELLOW);  // set color

	tft.setCursor(16, BotLine);		// Now display softbutton labels at bottom
	tft.println(F("HOME"));
	tft.setCursor(96, BotLine);
	tft.println(F("BACK"));
	tft.setCursor(179, BotLine);
	tft.println(F("RUN"));
	tft.setCursor(244, BotLine);
	tft.println(F("ResCHt"));

	tft.setCursor(252, 125);	// Display softbutton side labels
	tft.println(F("HEIGHT"));
	tft.setTextSize(2);		// Switch to larger font size for + and -
	tft.setCursor(282, 88);
	tft.println(F("+"));
	tft.setCursor(282, 168);
	tft.println(F("-"));
}

// Display the Setup Speed Screen
byte DisplaySetupSpeed() {

	int Indent2 = 40;
	int BotLine = 216;
	int StatusTop = 75;

	tft.fillScreen(ILI9341_NAVY);  // Clear old screen

	DrawLights();		// Draw indicator lights at top right on screen
	DrawButtons();		// Draw soft button outlines on display

	tft.setTextColor(ILI9341_WHITE);  // Preliminary text setup
	tft.setFont(&FreeMonoBold12pt7b);
	tft.setTextSize(0);

	tft.setCursor(20, StatusTop + 20);
	tft.println(F("Edit Max Speed:"));		// Large font just for header

	tft.setCursor(70, 165);		// Write Current Speed Setting on screen while in big font
	tft.setTextSize(2);
	SprintAnInt("", SetSpeed);


	tft.setFont(&FreeMono9pt7b);	// Switch to smaller font for all else
	tft.setTextSize(0);
	tft.setCursor(10, 15);
	tft.println(F("Setup Speed"));

	tft.setCursor(18, StatusTop + 45);
	tft.println(F("(0 to 99%)"));

	tft.setTextColor(ILI9341_YELLOW);  // set color

	tft.setCursor(16, BotLine);		// Now display softbutton labels at bottom
	tft.println(F("HOME"));
	tft.setCursor(96, BotLine);
	tft.println(F("BACK"));
	tft.setCursor(179, BotLine);
	tft.println(F("RUN"));

	tft.setCursor(257, 125);	// Display softbutton side labels
	tft.println(F("SPEED"));
	tft.setTextSize(2);		// Switch to larger font size for + and -
	tft.setCursor(282, 88);
	tft.println(F("+"));
	tft.setCursor(282, 168);
	tft.println(F("-"));
}

// Display the Run Screen
byte DisplayRun() {

	int Indent2 = 40;
	int BotLine = 216;
	int StatusTop = 75;

	tft.fillScreen(ILI9341_NAVY);  // Clear old screen

	DrawLights();		// Draw indicator lights at top right on screen
	DrawButtons();		// Draw soft button outlines on display

	tft.setTextColor(ILI9341_WHITE);  // Preliminary text setup
	tft.setFont(&FreeMonoBold12pt7b);
	tft.setTextSize(0);

	tft.setCursor(25, StatusTop);
	tft.println(F("RUN Status:"));		// Large font just for header

	tft.setFont(&FreeMono9pt7b);	// Switch to smaller font for all else
	tft.setCursor(10, 15);
	tft.println(F("RUN"));

	tft.setCursor(Indent2, StatusTop + 25);		// Print home status elements
	CalcRunHeight();
	sprintfloat("Current Ht: ", RunHeight);		// Call special function that will parse float and print to screen

	tft.setCursor(Indent2, StatusTop + 50);
	sprintfloat("Set Height: ", SetHeight);

	tft.setCursor(Indent2, StatusTop + 75);
	if (!Direction) {
		tft.println(F("Direction:   Down"));
	}
	else {
		tft.println(F("Direction:   Up"));
	}

	tft.setCursor(Indent2, StatusTop + 100);
	SprintAnInt("Speed 0-99%:", SetSpeed);		// Routine will do a conversion of speed to int

	tft.setTextColor(ILI9341_YELLOW);  // set color

	tft.setCursor(17, BotLine);		// Now display softbutton labels at bottom
	tft.println(F("HOME"));
	tft.setCursor(91, BotLine);
	tft.println(F("SETUP"));
	tft.setCursor(185, BotLine);
	tft.println(F("UP"));
	tft.setCursor(265, BotLine);
	tft.println(F("DN"));
	tft.setCursor(170, BotLine - 17);
	tft.println(F(" ___AUTO___"));

	// Now vertical softbuttons
	tft.setCursor(258, 85);
	tft.println(F("HALT"));
	tft.setCursor(268, 165);
	tft.println(F("RUN"));


}



// Emergency Halt Subroutine
void EmergencyHalt() {
	digitalWrite(A0, LOW);	// Turn off 48 Volts instantly
	VOn48 = LOW;		// Have to set the indicators to correctly indicate stopped conditioni
	Relay1On = LOW;
	RunActive = LOW;

	tft.fillScreen(ILI9341_NAVY);  // Clear old screen
	DrawLights();		// Draw indicator lights at top right on screen
	DrawButtons();		// Draw soft button outlines on display

	tft.setTextColor(ILI9341_WHITE);  // Preliminary text setup
	tft.setFont(&FreeMonoBold12pt7b);
	tft.setTextSize(3);

	tft.setCursor(25, 100);
	tft.println(F("HALTED"));		// Large font for emphasis
	tft.setTextSize(0);
	tft.setCursor(40, 180);
	tft.println(F("Press any button"));
	AnyButtonPush = LOW;
	while (!AnyButtonPush) {
		// Do nothing important and wait for a button to be pushed.  But have to allow interrupts to work!
		tft.setCursor(40, 180);
		tft.println(F("Press any button"));
	}
// Then return to normal processing
}

// Calculate RunHeight variable based on ShaftCount setting
void CalcRunHeight() {
	RunHeight = 10.0 + (ShaftCount * 0.2565);
}

// Update displayed screen
void DisplayUpdate() {
	if (ActiveScreen[0]) {
		DisplayHome();
	}
	else if (ActiveScreen[1]) {
		DisplaySetup1();
	}
	else if (ActiveScreen[2]) {
		DisplaySetupHeight();
	}
	else if (ActiveScreen[3]) {
		DisplaySetupSpeed();
	}
	else if (ActiveScreen[5]) {
		DisplayManual();
	}
}

// Draw display soft buttons (SB) on the screen
byte DrawButtons() {
	int SBStartX[] = { 14, 93, 172, 251 };
	int SBStartY[] = { 55, 134 };
	byte k; 

	for (k = 0; k < 4; ++k) {	//Print each horizontal softbutton
		tft.fillRoundRect(SBStartX[k], 225, 51, 40, 15, ILI9341_IVORY);
	}

	for (k = 0; k < 2; ++k) {
		tft.fillRoundRect(305, SBStartY[k], 40, 51, 15, ILI9341_IVORY);  // Print vertical softbuttons
	}
}

// Draw screen led lights
byte DrawLights() {

	int V48Color = ILI9341_RED;		// Default light colors
	int RunColor = ILI9341_DARKGREY;
	int LimitColor = ILI9341_GREEN; 

	const int YCenter = 12;
	const int YText = 34;
	const int CRadius = 8;

	if (VOn48) {				// Set the colors according to actual status
		V48Color = ILI9341_GREEN;
	}

	if (RunActive == 1) {				
		RunColor = ILI9341_YELLOW;
	}
	if (!Limit) {				// Trip limit when line goes low
		LimitColor = ILI9341_RED;
	}

	tft.fillCircle(180, YCenter, CRadius, V48Color);  // Draw the colored lights in correct color
	tft.fillCircle(230, YCenter, CRadius, RunColor);
	tft.fillCircle(280, YCenter, CRadius, LimitColor);


	tft.setTextColor(ILI9341_WHITE);		// Setup to print light labels
	tft.setFont(&FreeMono9pt7b);
	tft.setTextSize(0);

	tft.setCursor(164, YText);				// Print light labels
	tft.println(F("48V"));

	tft.setCursor(214, YText);
	tft.println(F("Run"));

	tft.setCursor(255, YText);
	tft.println(F("Limit"));

}

// Check Limit switch and take action as appropriate
void CheckLimit() {		// Three Steps which are mutually exclusive and usually only need to be run once

	// STEP1
	if (RunActive & (!digitalRead(LimitInput)) & Step1) {	// Only check for limits if actively running.  If D8 is low, a limit switch has been opened
		Limit = LOW;		// Change Limit state to fault/red
		DrawLights();			// Change display
		LimitDirection = Direction;		// Capture which direction the mast was moving
		NeedMotorTurns = 3;			// Assume that three turns of the motor shaft in other direction will clear magnetic trip of limit switch
		Step1 = LOW;
		Step2 = HIGH;				// Now we're ready for the second step
		Serial.println(F("Step1 Complete"));
	}

	// STEP2 - Now further processing of limit recovery
	if (!RunActive & (Direction != LimitDirection) & Step2) {	// Run has been stopped and direction changed
		Limit = HIGH; 
		DrawLights();	// Reset the limit indicators on the display
		pinMode(LimitInput, OUTPUT);	// We're going to use the port as an output to force release of the brake
		digitalWrite(LimitInput, HIGH);
		LimitMode = HIGH;				// Set flag that limit port is in temporary write mode
		Step2 = LOW;					// Don't need to run this again and ready for 3rd step
		Step3 = HIGH;
		Serial.println(F("Step2 Complete"));
	}

	// STEP3 - Now to check and see if it's time to return limit system to read after a few motor turns
	if (LimitMode & RunActive & Step3) {
		if (!NeedMotorTurns) {
			pinMode(LimitInput, INPUT);		// Return limit sensing port to read after a few turns of the motor shaft.
			LimitMode = LOW;				// Reset limit port flag to signify it's in read
			Step3 = LOW;
			Step1 = HIGH;		// Limit has been dealt with so return system to armed condition
			Serial.println(F("Step3 Complete"));
		}
	}
}

//Reset the active screen array
int ResetActiveScreen() {       // Reset the ActiveScreen array to null
	for (int i = 0; i < 6; i++) {
		ActiveScreen[i] = LOW;
	}
}

// Reset the push-buttons array
int ResetButtons() {			// Reset the buttons
		for (int i = 0; i < 6; i++) {
		ButtonPushed[i] = HIGH;
	}
}

// Specific to reset of Current Height counter menu item.  Reset flag to skip counter reset, and erase any reminder by printing same message in background color.
void NullGlitchReset() {
	tft.setTextColor(ILI9341_NAVY);  // Erase 2nd press note that may be present by writing in background color
	tft.setFont(&FreeMono9pt7b);
	tft.setTextSize(0);
	tft.setCursor(1, 190);
	tft.println(F("Press ResCHt to zero Cur.Ht."));
	GlitchReset = LOW;
}

// SPRINTFLOAT - Print String and Floating Point Number at current cursor
byte sprintfloat(char *StringtoPrint, float CurrentHeight) {  // Separate components of Float, and print resulting string at cursor

	double WholeNum, FractionNum, FractWhole;
	int intWhole, intFract;
	char PrnString[16];

	FractionNum = modf(CurrentHeight, &WholeNum);		// Split off whole and fractional parts of float
	intWhole = int(WholeNum);
	FractionNum = FractionNum * 100;					// Move the decimal point 2 places to right on resulting fractional part

	FractionNum = modf(FractionNum, &FractWhole);		// Repeat to get a two digit whole number for fractional part
	intFract = int(FractWhole);

	sprintf(PrnString, "%s %d.%d", StringtoPrint, intWhole, intFract);  // Print the whole mess to string PrnString
	tft.println(PrnString);

}

// SPRINTANINT - Print String and Integer at current cursor
byte SprintAnInt(char* StringtoPrint, int IntToPrint) {  // Separate components string to print at cursor
	
	char PrnString[16];

	sprintf(PrnString, "%s %d", StringtoPrint, IntToPrint);  // Print the elements to PrnString
	tft.println(PrnString);
}

// Display Motor Speed
byte DisplayMotorSpeed(int ms, int StartX, int StartY) {	

	tft.fillRect(StartX, StartY, 200, 25, ILI9341_NAVY);  // Erase old speed first.  Note that args are (x, y, len x, len y)
	tft.setTextColor(ILI9341_WHITE);
	tft.setFont(&FreeMono9pt7b);
	tft.setTextSize(0);
	tft.setCursor(StartX +5, StartY +15);
	SprintAnInt("Speed 0-100:", ms);
}

// Display Set Height
byte DisplaySetHeight(float sheight) {
	tft.fillRect(65, 130, 200, 40, ILI9341_NAVY);  // Erase old height first.  Note that args are (x, y, len x, len y)
	tft.setTextColor(ILI9341_WHITE);
	tft.setFont(&FreeMonoBold12pt7b);
	tft.setTextSize(2);
	tft.setCursor(70, 165);
	sprintfloat("", sheight);
}

// Display Set Speed
byte DisplaySetSpeed(int sspeed) {
	tft.fillRect(65, 130, 200, 40, ILI9341_NAVY);  // Erase old speed first.  Note that args are (x, y, len x, len y)
	tft.setTextColor(ILI9341_WHITE);  // Text setup
	tft.setFont(&FreeMonoBold12pt7b);
	tft.setCursor(70, 165);		// Write Current Speed Setting on screen while in big font
	tft.setTextSize(2);
	SprintAnInt("", sspeed);
}

// Display Current Height -- Based on global variables ShaftCount and RunHeight
byte DisplayCurrentHeight(float CurHt) {

	tft.fillRect(155, 85, 200, 25, ILI9341_NAVY);  // Erase old count first.  Note that args are (x, y, len x, len y)
	tft.setFont(&FreeMono9pt7b);
	tft.setTextSize(0);
	tft.setCursor(162, 100);
	tft.setTextColor(ILI9341_WHITE);
	tft.setCursor(162, 100);
	CalcRunHeight();			// Calculate Running Height from ShaftCount
	// Serial.println(CurHt);	// Debug display of ShaftCount
	sprintfloat("", RunHeight);
}

// Set the speed control PWM output accordingly.  This is what does the work of getting the motor to run.
byte SetMotorSpeed(int ms, int mset) {		// Input for ms and mset is 0 to 100 (percent).  ms is the speed to attain, mset is the speed setting in the computer
	
	// Now to get 256 count equivalent for PWM control of motor.
	// Formula is: mPWM = (1.30 * ms) + 60;
	/* This would be 2.55 to make it 100% on, but the motor control circuit doesn't seem to work above 4.0 volts on the input.
	   Therefore, we're scaling back to about 80% which should yield just short of 4 volts.

	   Actual effective control range on the motor is from about 1.2volts to 3.95 volts.
	*/

	//	Serial.print(F("Pulse Width Mod = "));  // Just looking at output for now
	//	Serial.println(mPWM);

	Serial.println(F("Speed to attain: "));  // Lets take a look
	Serial.println(ms);
	Serial.println(F("Set Speed: "));  // Lets take a look
	Serial.println(mset);



	// Motor controller speed is about 2X faster going up than down due to hardware design.  So let's divide speed going up by 2 to make things even.
	if (Direction) {		// Up is high, down is low
		mset = mset / 2;
		ms = ms / 2;
	//	Serial.println(mset);
	}

	// Write the PWM speed to the pin with a startup or shutdown ramp
	
	if (ms == mset) {		// motor is being turned on and speed should be ramped up
		for (int i = 0; i <= mset; i++) {
			mPWM = (1.30 * i) + 60;
			analogWrite(SpeedPin, mPWM);
			if (Direction) {
				delay(60);		// Have to slow down ramp because it's been divided by 2 for up direction
			}
			else {
				delay(30);
			}
		}		// Motor is now running at assigned speed
	}

	if (ms > 0 && ms < mset) {		// motor is being slowed down.  Most likely because it is approaching targeted height.
		for (int i = 0; i <= (mset-ms); i++) {
			mPWM = (1.30 * i) + 60;
			analogWrite(SpeedPin, mPWM);
			if (Direction) {
				delay(60);		// Have to slow down ramp because it's been divided by 2 for up direction
			}
			else {
				delay(30);
			}
		}		// Motor is now running at reduced speed lower than SetSpeed in computer
	}


	if (ms == 0) {		// motor is being turned off and speed should be ramped down
		for (int i = mset; i > 0; i--) {
			mPWM = (1.30 * i) + 60;
			analogWrite(SpeedPin, mPWM);
			if (Direction) {
				delay(60);		// Have to slow down ramp because it's been divided by 2 for up direction
			}
			else {
				delay(30);
			}
		}
		EEPROM.put(0, ShaftCount);	// Save turning off height in special location 0 in EEPROM
		OldShaftCount = ShaftCount;
	}			// Motor is now off

	Serial.println(mPWM);
}